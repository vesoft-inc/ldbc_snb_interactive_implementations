MATCH (person)-[:KNOWS*1..2]-(friend)-[:IS_LOCATED_IN]->(city:Place)-[:IS_PART_OF]->(country:Place)
WHERE id(person) == $personId AND id(friend) != $personId AND country.Place.name != $countryXName AND country.Place.name != $countryYName
WITH DISTINCT friend
MATCH (friend)<-[:POST_HAS_CREATOR|COMMENT_HAS_CREATOR]-(message)-[:IS_LOCATED_IN]->(country)
WHERE country.Place.name IN [$countryXName, $countryYName] AND
    CASE
    WHEN message.`Comment`.creationDate < $startDate+Duration({days:$durationDays}) AND message.`Comment`.creationDate >= $startDate THEN true
    WHEN message.Post.creationDate < $startDate+Duration({days:$durationDays}) AND message.Post.creationDate >= $startDate THEN true
    ELSE false
    END
WITH friend,
     CASE WHEN country.Place.name == $countryXName THEN 1 ELSE 0 END AS messageX,
     CASE WHEN country.Place.name == $countryYName THEN 1 ELSE 0 END AS messageY
WITH friend, sum(messageX) AS xCount, sum(messageY) AS yCount
WHERE xCount>0 AND yCount>0
RETURN tointeger(substr(id(friend), 2)) AS friendId,
  friend.Person.firstName AS friendFirstName,
  friend.Person.lastName AS friendLastName,
  xCount,
  yCount,
  xCount + yCount AS xyCount
ORDER BY xyCount DESC, friendId ASC
LIMIT 20
