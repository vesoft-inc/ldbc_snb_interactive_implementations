MATCH (person:Person)-[:KNOWS*2]-(friend)-[:IS_LOCATED_IN]->(city:Place)
WHERE id(person) == "p-10995116542546" AND id(friend) != "p-10995116542546" AND NOT (friend)-[:KNOWS]-(person)
WITH person, city, friend, datetime(friend.Person.birthday) as birthday
WHERE  (birthday.month==10 AND birthday.day>=21) OR
        (birthday.month==(10%12)+1 AND birthday.day<22)
WITH DISTINCT friend AS f, city AS c, person AS p
OPTIONAL MATCH (f)<-[:POST_HAS_CREATOR]-(post:Post)
WITH f AS friend, c AS city, collect(post) AS posts, count(post) AS postCount, p AS person
UNWIND posts AS post
OPTIONAL MATCH (person)-[e:HAS_INTEREST]->()<-[:HAS_TAG]-(post)
WITH friend, city, postCount, count(e) AS commonPostCount
RETURN toInteger(substr(id(friend), 2)) AS personId,
       friend.Person.firstName AS personFirstName,
       friend.Person.lastName AS personLastName,
       commonPostCount - (postCount - commonPostCount) AS commonInterestScore,
       friend.Person.gender AS personGender,
       city.Place.name AS personCityName
ORDER BY commonInterestScore DESC, personId ASC
LIMIT 10